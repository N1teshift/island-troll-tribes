package Cure

// Standard library imports:
import Assets
import Orders
import ClosureEvents
import InstantDummyCaster
import ChannelAbilityPreset

// Local imports:
import Items
import LocalAssets
import LocalObjectIDs
import LocalItemObjEditing

public constant ICON  = Icons.bTNLesserClarityPotion
let MODEL = LocalItems.potionGreenSmall
public constant NAME ="Cure Potion"
public constant CAST_RANGE = 500.
public constant TARGET_ALLOWED = "friend,organic,self"
public constant TT_CURE = "This potion is said to be able to cure anything."

@compiletime function createAbil() returns ChannelAbilityPreset
    return createItemChannelCast(ABILITY_CURE_ALL_ITEM)
        ..setName(NAME)
        ..presetTargetTypes(Targettype.UNIT)
        ..setTargetsAllowed(1, TARGET_ALLOWED)
        ..setCastRange(1, CAST_RANGE)
        ..setManaCost(1, 0)
        ..setCooldown(1, 0)
        // Ensure the order ID is one that targets units.
        // TODO: Remove this once order IDs can be generated correctly.
        // ..presetBaseOrderID(lvl -> "cripple")


@compiletime function createCure() returns ItemDefinition
    return    createPerishableItem(ITEM_CURE_ALL)
        ..setNameEnhance(NAME)
        ..setInterfaceIcon(ICON)
        ..setModelUsed(MODEL)
        ..setHotkey("S")
        ..setTooltipBasic(NAME)
        ..setTooltipExtended(TT_CURE)
        ..setLumberCost(8)
        ..setAbilities(commaList(ABILITY_CURE_ALL_ITEM))
        ..setScalingValue(0.5)
        ..setCooldownGroup(commaList(ABILITY_CURE_ALL_ITEM))
        ..setStockReplenishInterval(120)
        ..setNumberofCharges(2)
        ..setButtonPositionX(1)
        ..setButtonPositionY(1)
 

init
    EventListener.onTargetCast(ABILITY_CURE_ALL_ITEM) (unit caster, unit target) ->
        InstantDummyCaster.castTarget(caster.getOwner(), ABILITY_CURE_ALL, 1, Orders.autodispel, target)
