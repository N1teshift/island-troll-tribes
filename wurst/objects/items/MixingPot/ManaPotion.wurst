package ManaPotion

// Standard library imports:
import Assets
import ClosureEvents
import ChannelAbilityPreset

// Local imports:
import Items
import LocalAssets
import ToolTipsUtils
import LocalObjectIDs
import LocalItemObjEditing

public constant MANA_RESTORED = 80.
public constant CAST_RANGE = 400.
public constant NAME = "Mana Potion"
public constant ICON = Icons.bTNPotionBlueSmall

public constant TT_MANA_POTION  = "A dilute potion made from Athelas Seed. "+
    "Instantly restores {0} Mana point to a target ally."
    .format(MANA_RESTORED.toToolTipLightBlue())

public constant TARGETS_ALLOWED = "friend,ground,hero,invulnerable,organic,self,vulnerable,item"

@compiletime function createAbil() returns ChannelAbilityPreset
    return createItemChannelCast(ABILITY_MANA_POTION)
        ..setName(NAME)
        ..presetTargetTypes(Targettype.UNIT)
        ..setTargetsAllowed(1, TARGETS_ALLOWED)
        ..setCastRange(1, CAST_RANGE)
        ..setManaCost(1, 0)
        ..setCooldown(1, 0)

@compiletime function createManaPotion() returns ItemDefinition
    return createPerishableItem(ITEM_MANA_POTION)
        ..setNameEnhance(NAME)
        ..setInterfaceIcon(ICON)
        ..setModelUsed(LocalItems.potionBlueLesser)
        ..setHotkey("W")
        ..setTooltipExtended(TT_MANA_POTION)
        ..setLumberCost(10)
        ..setAbilities(commaList(ABILITY_MANA_POTION))
        ..setScalingValue(0.7)
        ..setCooldownGroup(commaList(ABILITY_MANA_POTION))
        ..setStockReplenishInterval(120)
        ..setNumberofCharges(2)



function doubleClickSelfCast(unit caster, item targetItem)
    if not caster.hasItem(targetItem) or targetItem.getTypeId()!=ITEM_MANA_POTION
        let usedItem = caster.getItemById(ITEM_MANA_POTION)
        usedItem.setCharges(usedItem.getCharges()+1)
        return
    caster.addMana(MANA_RESTORED)
    flashEffect(Abilities.aImaTarget, caster, "origin")

init
    EventListener.onTargetCast(ABILITY_MANA_POTION) (unit caster, unit target) ->
        target.addMana(MANA_RESTORED)
        flashEffect(Abilities.aImaTarget, target, "origin")

    EventListener.add(EVENT_PLAYER_UNIT_SPELL_CAST)->
        if EventData.getSpellAbilityId() == ABILITY_MANA_POTION and EventData.getSpellTargetItem() !=null
            doubleClickSelfCast(EventData.getSpellAbilityUnit() , EventData.getSpellTargetItem())