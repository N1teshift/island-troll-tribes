package ShieldsDefinition

// Standard lib Imports:
import AbilityObjEditing
import Assets
import ItemObjEditing
import LinkedList
import ObjectIdGenerator
import ObjectIds

// Local Imports:
import LocalObjectIDs
import ToolTipsUtils
import LocalAssets
import Items
import ColorUtils
import TrollProtector

// I've been really lazy down here

constant COOLDOWN = 15.
constant DURATION = 2.
public constant MAKRURA_CARAPACE_COOLDOWN = 7.

constant ABILITY_SHIELD_AMUL = 'A09N'

constant SHIELD_DAMAGE_BLOCK0 = 0.
constant SHIELD_DAMAGE_BLOCK3 = 3.
constant SHIELD_DAMAGE_BLOCK5 = 5.
constant SHIELD_DAMAGE_BLOCK7 = 7.
constant SHIELD_DAMAGE_BLOCK10 = 10.

public constant BASIC_SHIELD_TOOLTIP = "A Basic Shield, reduce damage taken by {0}, gives {1} magic resistance.".format(SHIELD_DAMAGE_BLOCK3.toToolTipOrange(), 0.10.toToolTipLightBlue())
public constant BONE_SHIELD_TOOLTIP  = "A Bone Shield, reduce damage taken by {0}, gives {1} armor and {2} magic resistance.".format(SHIELD_DAMAGE_BLOCK5.toToolTipOrange(), 1..toToolTipGreen(), 0.10.toToolTipLightBlue())
public constant IRON_SHIELD_TOOLTIP  = "An Iron Shield, reduce damage taken by {0}, gives {1} armor and {2} magic resistance.".format(SHIELD_DAMAGE_BLOCK7.toToolTipOrange(), 2..toToolTipGreen(), 0.10.toToolTipLightBlue())
public constant STEEL_SHIELD_TOOLTIP = "A Steel Shield, reduce damage taken by {0}, gives {1} armor and {2} magic resistance.".format(SHIELD_DAMAGE_BLOCK10.toToolTipOrange(), 4..toToolTipGreen(), 0.10.toToolTipLightBlue())
                                    +" Has a bash ability, silences and disarms the target."+makeToolTipDuration(DURATION, COOLDOWN)
public constant BATTLE_SHIELD_TOOLTIP = "A Battle Shield, reduce damage taken by {0}, gives {1} armor.".format(SHIELD_DAMAGE_BLOCK10.toToolTipOrange(), 7..toToolTipGreen())
                                    +" Has a bash ability, silences and disarms the target."+makeToolTipDuration(DURATION, COOLDOWN)
public constant TROLL_PROTECTOR_TOOLTIP = "An Iron shield enhanced with magic, reduce damage taken by {0}. ".format(SHIELD_DAMAGE_BLOCK7.toToolTipOrange())+
                                    "\nTroll Protector Aura".color(COLOR_GREEN)+":"+
                                    "\n"+TROLL_PROTECTOR_TT_EXT

public constant MAKRURA_CARAPACE_TOOLTIP = "A Makrura's carapace of a medium size, can be used as a shield. "+ 
                                            "\nProvides {0} armor".format(3..toToolTipGreen())+" and {0} magic resistance. ".format(0.10.toToolTipLightBlue())+
                                            "Carapace".color(COLOR_TURQUOISE)+
                                            ": \nAllows wearer to block an attack once every {0} ".format(MAKRURA_CARAPACE_COOLDOWN.toToolTipBlue())+
                                            "seconds returning premitigation damage to the attacker."
constant ABILITY_DUMMY2_SHIELD_BLOCK_ID = compiletime(ABIL_ID_GEN.next())
constant ABILITY_DUMMY_SHIELD_BLOCK_ID = compiletime(ABIL_ID_GEN.next())
public constant ABILITY_BASIC_SHIELD_BLOCK_ID = compiletime(ABIL_ID_GEN.next())
public constant ABILITY_BONE_SHIELD_BLOCK_ID  = compiletime(ABIL_ID_GEN.next())
public constant ABILITY_IRON_SHIELD_BLOCK_ID  = compiletime(ABIL_ID_GEN.next())
public constant ABILITY_STEEL_SHIELD_BLOCK_ID = compiletime(ABIL_ID_GEN.next())
public constant ABILITY_BATTLE_SHIELD_BLOCK_ID = compiletime(ABIL_ID_GEN.next()) //lazy design with battle shield
public constant ABILITY_STEEL_SHIELD_BASH_ID  = compiletime(ABIL_ID_GEN.next())
public constant ABILITY_TROLL_PROTECTOR_ID  = compiletime(ABIL_ID_GEN.next())

constant BASIC_SHIELD_ABILITIES  = commaList(ABILITY_BASIC_SHIELD_BLOCK_ID)
constant BONE_SHIELD_ABILITIES   = commaList(ABILITY_BONE_SHIELD_BLOCK_ID, ABILITY_ARMOR_BONUS_1)
constant IRON_SHIELD_ABILITIES   = commaList(ABILITY_IRON_SHIELD_BLOCK_ID, ABILITY_ARMOR_BONUS_2)
constant STEEL_SHIELD_ABILITIES  = commaList(ABILITY_STEEL_SHIELD_BASH_ID, ABILITY_STEEL_SHIELD_BLOCK_ID, ABILITY_ARMOR_BONUS_4)
constant BATTLE_SHIELD_ABILITIES = commaList(ABILITY_STEEL_SHIELD_BASH_ID, ABILITY_IRON_SHIELD_BLOCK_ID, ABILITY_ARMOR_BONUS_7)
constant MAKRURA_CARAPACE_ABILITIES = commaList(ABILITY_ARMOR_BONUS_3)
constant MAKRURA_CARAPACE_DUMMY_ABILITIES = commaList(ABILITY_CARAPACE_DUMMY, ABILITY_ARMOR_BONUS_3)
constant TROLL_PROTECTOR_ABILITIES = commaList(ABILITY_IRON_SHIELD_BLOCK_ID, ABILITY_TROLL_PROTECTOR_AURA)

class AttackTypes
    static constant melee   = 1
    static constant ranged  = 2
    static constant special = 4
    static constant spells  = 8

public class Shield
    int id = 0
    int abilId = 0
    int goldCost = 0
    real scale = 0
    real ignoreDamage = 0
    string name = ""
    string tooltip = ""
    string iconPath = "ReplaceableTextures\\CommandButtons\\BTN"
    string abilities = ""
    string modelPath = ""

    construct(int id, string name, int abilId, real ignoreDamage, string tooltip, string iconPath, string modelPath, string abilities, int goldCost)
        this.scale = 0.8
        this.id = id
        this.name = name
        this.abilId = abilId
        this.tooltip = tooltip
        this.goldCost = goldCost
        this.iconPath += iconPath+".blp"
        this.ignoreDamage = ignoreDamage
        this.abilities = abilities
        if id != ITEM_BATTLE_SHIELD
            this.abilities += "," + ABILITY_MAGIC_RESIST.toRawCode() // 10% magic resistance ability
        this.modelPath = modelPath

    construct(int id, string name, int abilId, real ignoreDamage, string tooltip, string iconPath, string modelPath, string abilities, int goldCost, real scale)
        this.scale=scale
        this.id = id
        this.name = name
        this.abilId = abilId
        this.tooltip = tooltip
        this.goldCost = goldCost
        this.iconPath += iconPath+".blp"
        this.ignoreDamage = ignoreDamage
        this.abilities = abilities
        if id != ITEM_BATTLE_SHIELD
            this.abilities += "," + ABILITY_MAGIC_RESIST.toRawCode() // 10% magic resistance ability
        this.modelPath = modelPath

    function buildShield()
        buildItem()
        buildReduceDamageSpell()

    function buildItem()
        new ItemDefinition(id, 'bspd')
        ..setName(name)
        ..setAbilities(abilities)
        ..setTooltipExtended(tooltip)
        ..setModelUsed(modelPath)
        ..setGoldCost(0)
        ..setInterfaceIcon(iconPath)
        ..setDescription(tooltip)
        ..setDroppedWhenCarrierDies(true)
        ..setScalingValue(scale)
        ..setActivelyUsed(true)
        ..setCooldownGroup("defend")
        ..setHitPoints(75)
        ..setLumberCost(goldCost)
        ..setLevelUnclassified(0)
        ..setPerishable(false)
        ..setPriority(0)
        ..setTooltipBasic("Trade for "+name)

    function buildReduceDamageSpell()
        new AbilityDefinitionHardenedSkin(abilId)
        ..setName(name+" block")
        ..setRequirements("")
        ..setChancetoReduceDamage(1, 100.0)
        ..setIgnoredDamage(1, ignoreDamage)
        ..setMinimumDamage(1, 1)
        ..setItemAbility(true)

function getShield() returns LinkedList<Shield>
    return asList(
        new Shield(ITEM_SHIELD        , "Basic Shield"  , ABILITY_BASIC_SHIELD_BLOCK_ID  , SHIELD_DAMAGE_BLOCK3 , BASIC_SHIELD_TOOLTIP  , "SteelArmor"         , LocalItems.shield      , BASIC_SHIELD_ABILITIES , 13),
        new Shield(ITEM_BONE_SHIELD   , "Bone Shield"   , ABILITY_BONE_SHIELD_BLOCK_ID   , SHIELD_DAMAGE_BLOCK5 , BONE_SHIELD_TOOLTIP   , "ImprovedUnholyArmor", LocalItems.boneShield  , BONE_SHIELD_ABILITIES  , 20),
        new Shield(ITEM_IRON_SHIELD   , "Iron Shield"   , ABILITY_IRON_SHIELD_BLOCK_ID   , SHIELD_DAMAGE_BLOCK7 , IRON_SHIELD_TOOLTIP   , "HumanArmorUpOne"    , LocalItems.ironShield  , IRON_SHIELD_ABILITIES  , 46),
        new Shield(ITEM_STEEL_SHIELD  , "Steel Shield"  , ABILITY_STEEL_SHIELD_BLOCK_ID  , SHIELD_DAMAGE_BLOCK10 , STEEL_SHIELD_TOOLTIP  , "ThoriumArmor"       , LocalItems.steelShield , STEEL_SHIELD_ABILITIES , 66),
        new Shield(ITEM_BATTLE_SHIELD , "Battle Shield" , ABILITY_BATTLE_SHIELD_BLOCK_ID , SHIELD_DAMAGE_BLOCK10 , BATTLE_SHIELD_TOOLTIP , "ArcaniteArmor"      , LocalItems.battleShield, BATTLE_SHIELD_ABILITIES , 82),
        new Shield(ITEM_MAKRURA_CARAPACE , "Makrura Carapace" , ABILITY_DUMMY_SHIELD_BLOCK_ID  , SHIELD_DAMAGE_BLOCK0 , MAKRURA_CARAPACE_TOOLTIP , "MakruraCarapace"      , LocalItems.makruraCarapace, MAKRURA_CARAPACE_ABILITIES , 41, 1.0),
        new Shield(ITEM_MAKRURA_CARAPACE_DUMMY , "Makrura Carapace" , ABILITY_DUMMY2_SHIELD_BLOCK_ID  , SHIELD_DAMAGE_BLOCK0 , MAKRURA_CARAPACE_TOOLTIP , "MakruraCarapace", LocalItems.makruraCarapace, MAKRURA_CARAPACE_DUMMY_ABILITIES , 41, 1.0),
        new Shield(ITEM_TROLL_PROTECTOR , "Troll Protector" , ABILITY_TROLL_PROTECTOR_ID  , SHIELD_DAMAGE_BLOCK7 , TROLL_PROTECTOR_TOOLTIP , "TrollProtector", LocalItems.trollProtector, TROLL_PROTECTOR_ABILITIES , 41)
    )



@compiletime function createShieldSpell()
    new AbilityDefinitionBrewmasterDrunkenHaze(ABILITY_STEEL_SHIELD_BASH_ID)
    ..setArtTarget(Abilities.replenishHealthCaster)
    ..setEffectSound("MetalHeavyBashFlesh")
    ..setMissileArt("")
    ..presetAttacksPrevented(lvl -> AttackTypes.melee.bitOr(AttackTypes.spells))
    ..setChanceToMiss(1, 0)
    ..setAttackSpeedModifier(1, 0)
    ..setMovementSpeedModifier(1, 0)
    ..setAreaofEffect(1, 0)
    ..setBuffs(1, "BNsi")
    ..setCastRange(1, 100)
    ..setCooldown(1, COOLDOWN)
    ..setDurationHero(1, DURATION)
    ..setDurationNormal(1, DURATION)
    ..presetTargetsAllowed(_ -> commaList(
        TargetsAllowed.ground,
        TargetsAllowed.neutral,
        TargetsAllowed.enemies,
        TargetsAllowed.organic,
        TargetsAllowed.nonancient
    ))
    ..setHeroAbility(false)
    ..setLevels(1)
    ..setItemAbility(true)
    ..setManaCost(1, 0)
    ..setName("Shield Bash")

    createItemBerserkCast(ABILITY_CARAPACE_DUMMY)
        ..setName("Makrura Carapace cooldown")
        ..setCooldown(1, MAKRURA_CARAPACE_COOLDOWN)





@compiletime function buildShield()
    getShield().forEach(shield -> shield.buildShield())
