package WeaponsOfChoiceSlot

import Classes
import LocalObjectIDs
import HashMap
import OnUnitEnterLeave
import Items
import LocalAssets
import UnitExtensions
import ChannelAbilityPreset
import Orders
import ClosureEvents
import ClosureTimers
import InventoryEvent
import HashSet
import BushSpawns
import ItemObjEditing
import ClosuresForItems
import ItemExtensions
import IdListConstant
import ColorUtils
import LinkedList
import QuickMake
import PlayerExtensions
import ObjectIDManager


public IterableMap<unit, WeaponsOfChoiceSlot> weaponsOfChoiceSlotInstances = new IterableMap<unit, WeaponsOfChoiceSlot>()

public let ITEM_ARMS_SLOT = compiletime(ITEM_ID_GEN.next())
    ..registerObjectID("ITEM_ARMS_SLOT")  

public let stolenItems = new HashSet<item>()
public let bearFoodList = asList(ITEM_BANANA, ITEM_HONEYCOMB, ITEM_COCONUT, ITEM_SCAVENGED_MUSHROOM)

let TT_NAME = "Arms Slot"
let TT_TOOLTIP = "A special slot you can carry a weapon or a shield in. "+
                "Gurubashi Warrior gains different abilities depending on weapons equipped."


@compiletime function createDummyItem() returns ItemDefinition
    return createBasicItem(ITEM_ARMS_SLOT)
            ..setName(TT_NAME)
            ..setCanBeDropped(true)
            ..setInterfaceIcon(LocalIcons.pASBTNWeaponsOfChoiceSlot)
            ..setTooltipExtended(TT_TOOLTIP)
            ..setCanBeSoldToMerchants(false)


public class WeaponsOfChoiceSlot
    unit hero = null
    item dummy = null
    bool pauseTriggers = false

    construct(unit _hero)
        hero = _hero
        dummy = createItem(ITEM_ARMS_SLOT, hero.getPos())..setVisible(false)
        nullTimer() -> 
            updateDummyItems()

    function updateDummyItems()
        pauseTriggers = true
        let currentArmsItems = getArmsItems()
        let currentDummyItems = getCurrentDummyItems()
        
        if currentArmsItems == 0      
            addDummyItem()

        if currentArmsItems > 0       
            removeDummyItem()

        doAfter(ANIMATION_PERIOD) ->                                
            pauseTriggers = false

    function getArmsItems() returns int
        int itemCount = 0
        let inventory = hero.getInventory()
        let equippedItems = hero.getOwner().getEquippedItems()
        inventory.addAll(equippedItems)
        for each in inventory
            if each.isAxeOrShield()
                itemCount+=1
        destroy inventory 
        destroy equippedItems              
        return itemCount

    function getCurrentDummyItems() returns int
        int currentDummyItems = 0
        let inventory = hero.getInventory()
        for each in inventory
            if each.getTypeId() == ITEM_ARMS_SLOT
                currentDummyItems+=1 
        destroy inventory       
        return currentDummyItems       


    function removeDummyItem()
        hero.removeItem(dummy)
        nullTimer() -> 
            dummy.setVisible(false)
        //print("removed dummy item")

    function addDummyItem()
        if hero.hasItem(dummy)
            //print("hero already has dummy")
            return                  
        let inventory = hero.getInventory()
        let equippedItems = hero.getOwner().getEquippedItems()
        inventory.addAll(equippedItems)
        while hero.isInventoryFull()
            if inventory.getLast().getTypeId() == ITEM_ARMS_SLOT
                inventory.remove(inventory.getLast())
            else
                //print("removed item")
                hero.removeItem(inventory.getLast())
        hero.addItemHandle(dummy)
        destroy equippedItems
        destroy inventory
        //print("added dummy item "+index.toString())
    
    function onTargetOrder(item itm) 
        if not itm.isAxeOrShield()
            return
        if hero.hasItem(itm) or itm == null
            //print("handled by stack system")
            return
        if hero.hasItemEquipped(itm)
            updateDummyItems()
            return



function onSub(unit troll)
    if not weaponsOfChoiceSlotInstances.has(troll)
        doAfter(ANIMATION_PERIOD)->
            if not weaponsOfChoiceSlotInstances.has(troll)
                weaponsOfChoiceSlotInstances.put(troll,new WeaponsOfChoiceSlot(troll))


function onPickup(unit troll, item itm)
    let instance = weaponsOfChoiceSlotInstances.get(troll)
    if instance == null
        return
    if instance.pauseTriggers == true
        return
    nullTimer() -> 
        instance.updateDummyItems()

function onDummyDrop(unit troll, item itm)
    nullTimer() -> 
        itm.setVisible(false)
    if not troll.isAlive()
        return
    let instance = weaponsOfChoiceSlotInstances.get(troll)
    if instance == null
        return
    if instance.pauseTriggers == true
        return    
    nullTimer() -> 
        //print(instance.pauseTriggers)
        //print("added dummy on drop")
        troll.addItemHandle(itm)

function onRevive(unit troll)
    let instance = weaponsOfChoiceSlotInstances.get(troll)
    if instance == null
        //print("null instance")
        return
    if instance.pauseTriggers == true
        //print("triggers paused")
        return    
    doAfter(ANIMATION_PERIOD) ->  
        instance.updateDummyItems()

function onQuickMakePriorEffect(unit caster)
    let instance = weaponsOfChoiceSlotInstances.get(caster)
    if instance == null
        return
    instance.pauseTriggers = true

function onQuickMakeAfterEffect(unit caster)
    let instance = weaponsOfChoiceSlotInstances.get(caster)
    if instance == null
        return
    instance.pauseTriggers = false
    doAfter(ANIMATION_PERIOD) ->
        instance.updateDummyItems() 
        

function onTargetOrder()
    let order = GetIssuedOrderId()
    let u = GetOrderedUnit()
    let target = GetOrderTargetItem()
    let instance = weaponsOfChoiceSlotInstances.get(u)
    if instance == null
        return
    if instance.pauseTriggers == true
        return
    if order != Orders.smart or not u.isInventoryFull() or not weaponsOfChoiceSlotInstances.has(u)
        return
    // Smart pickup system -> simulate picking up meat like an item if full inventory, like in Bannars StackNSplit system
    // Used to pick up ammo even if inventory's full
    // TODO: Currently only work when troll is in pick up range, make it so the troll move toward ammo even if inventory is full
    if (target != null
        and target.isPickupable()
        and u.hasItemById(ITEM_ARMS_SLOT))
        let collisionSize = u.getCollisionSize()
        let rangeSq = (150.0 + collisionSize).pow(2)
        if u.getPos().distanceToSq(target.getPos()) <= rangeSq
            doAfter(0.01) ->
                instance.onTargetOrder(target)


init
  
    EventListener.onCast(ABILITY_WARRIOR) (unit caster) ->
        onSub(caster)

    EventListener.add(EVENT_PLAYER_UNIT_PICKUP_ITEM) ->
        if GetManipulatedItem().isAxeOrShield()
            onPickup(GetManipulatingUnit(), GetManipulatedItem())

    EventListener.add(EVENT_PLAYER_UNIT_DROP_ITEM) ->
        if GetManipulatedItem().isAxeOrShield()
            onPickup(GetManipulatingUnit(), GetManipulatedItem())  

    EventListener.add(EVENT_PLAYER_UNIT_DROP_ITEM) ->
        if GetManipulatedItem().getTypeId() == ITEM_ARMS_SLOT
            onDummyDrop(GetManipulatingUnit(), GetManipulatedItem())           

    registerPlayerUnitEvent(EVENT_PLAYER_HERO_REVIVE_FINISH)->
        onRevive(GetTriggerUnit())  

    EventListener.add(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER, () -> onTargetOrder())  

    registerQuickMakePriorEffect() (unit target, int unitID) ->
        onQuickMakePriorEffect(target)

    registerQuickMakeAfterEffect() (unit target, int unitID) ->
        onQuickMakeAfterEffect(target)

